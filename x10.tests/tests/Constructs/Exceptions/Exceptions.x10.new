/*
 *
 * (C) Copyright IBM Corporation 2006
 *
 *  This file is part of X10 Test.
 *
 */
import harness.x10Test;;

/**
 * Test for x10 exceptions.
 */
public class Exceptions extends x10Test {
	var ok: boolean;
	public def run(): boolean = {
		ok = true;
		try {
			finish {
				async(here) {
					if (true) throw new x10.lang.Exception();
				}
			}
			System.out.println("Exception missed!");
			return false;
		} catch (var e: x10.lang.Exception) {
			//System.out.println("Async exception caught!");
			// expected
		}

		try {
			finish {
				var ret: future<int> = future(here) { m() };
			}
			//System.out.println("Exception in future missed!");
			// expected
		} catch (var me: x10.lang.MultipleExceptions) {
			System.out.println("Multiple Exception caught where there should be none!");
			return false;
		} catch (var e: x10.lang.Exception) {
			System.out.println("Caught future exception without force!");
			return false;
		}

		try {
			finish {
				try {
					future(here) { m() }.force();
					System.out.println("Force not thrown!");
					ok = false;
				} catch (var e: x10.lang.Exception) {
					// System.out.println("Force-caught!");
					// expected
				}
			}
		} catch (var e: x10.lang.Exception) {
			System.out.println("Force-rethrow caught!");
			return false;
		}

		try {
			finish {
				future(here) { m() }.force();
				System.out.println("Force did not throw!");
				ok = false;
			}
			System.out.println("Force not propagated!");
			return false;
		} catch (var me: x10.lang.MultipleExceptions) {
			System.out.println("Multiple Exception (force + async) caught, single expected!");
			return false;
		} catch (var e: x10.lang.Exception) {
			//System.out.println("Caught single exception transitively!");
			// expected
		}

		try {
			finish {
				async(here) { m(); };
				future(here) { m() }.force();
				System.out.println("Force did not throw!");
				ok = false;
			}
			System.out.println("Force not propagated!");
			return false;
		} catch (var me: x10.lang.MultipleExceptions) {
			//System.out.println("Multiple Exception (force + async) caught!");
			// expected
		} catch (var e: x10.lang.Exception) {
			System.out.println("Caught single exception where multiple was expected!");
			return false;
		}

		return ok;
	}

	static def m(): int = {
		if (true) throw new x10.lang.Exception();
		return 42;
	}

	public static def main(var args: Array[String](1)): void = {
		new Exceptions().execute();
	}
}
